---
layout: post
category : 风儿飞飞
---
{% include JB/setup %}

raspberryPi与arduino之间无线通讯最便宜的解决方案无疑是使用RF了。为了有更远的通讯距离，
我们采用了RF905进行通讯。

我测试用的是arduino UNO 首先将RF905与arduion进行连接。接收和发送各一个。


先看发送部分代码

第一步是初始化设置

<pre>

void setup()
{
    unsigned char i;
    
    nrf905=NRF905(NRF905_CSN);
    
    nrf905.init();

    /** register configuration */
    // user self-defined configuration
    nrf905.write_config(write_config_buf);

    Serial.begin(9600);

}

</pre>
NRF905用于设置RF接 arduino 的IO
init()将IO按照RF的要求进行初始化
代码如下
<pre>
NRF905::NRF905(int css)
{
    TXEN=5;
    TRX_CE=4;
    PWR=3;
    CSN=css;
    AM=9;
    DR=8;
    CD=7;
}

void NRF905::init(void)
{
    pinMode(CSN, OUTPUT);
	digitalWrite(CSN, HIGH); // Spi disable

	pinMode(DR, INPUT);	// Init DR for input

	pinMode(AM, INPUT);// Init AM for input

	pinMode(CD, INPUT);// Init CD for input

	pinMode(PWR, OUTPUT);
	digitalWrite(PWR, HIGH);// nRF905 power on

	pinMode(TRX_CE, OUTPUT);
	digitalWrite(TRX_CE, LOW);// Set nRF905 in standby mode

	pinMode(TXEN, OUTPUT);
	digitalWrite(TXEN, LOW);// set radio in Rx mode

	SPI.setBitOrder(MSBFIRST);
    SPI.setDataMode(SPI_MODE0);
    SPI.setClockDivider(SPI_CLOCK_DIV2);
    SPI.begin();

    /** wait for nrf905 STANDBY */
    delay(3);
}
</pre>

nrf905.write_config(write_config_buf)用于通过SPI设置RF的设置信息

write_config_buf的结构如下：
<pre>
unsigned char write_config_buf[CONF_LEN]={
    0x4C,                   //CH_NO,430MHZ
    0x0C,                   //output power 10db, resend disable, Current Normal operation
    0x44,                   //4-byte address
    0x20,0x20,              //receive or send data length 32 bytes
    0xCC,0xCC,0xCC,0xCC,    //receiving address
    0x58,                   //CRC enable,8bit CRC,external clock disable,16MHZ Oscillator
};
</pre>
我们使用的是430MHZ的频段所以第一个字节设置为0x4c
型号强度10db 不重发，正常功率 所以第二个字节设置为0x0c
地址宽度我们设置的收发都是4字节所以0x44
数据宽度我们设置的收发都是32
默认地址我们设置为了0xCC 0xCC 0xCC 0xCC
输出频率我们设置为4MHZ，没有外部时钟，同时905上的晶振设置为16MHZ
允许8位CRC校验

write_config的代码如下：
<pre>
void NRF905::write_config(unsigned char *conf_buf)
{
	digitalWrite(CSN,LOW);						// Spi enable for write a spi command
	/** send write configuration command */
	SPI.transfer(WC);
	for (int i=0;i<10;i++)	// Write configration words
	{
	   SPI.transfer(conf_buf[i]);
	}
	digitalWrite(CSN,HIGH);					// Disable Spi
}
</pre>

设置完成后就在主循环中发送了
代码如下：

<pre>
void loop()
{
      unsigned char i;

    /** transmit data packet with default TX Address */

  sendvalue="hello world  \r\n";
 
  for(i=0; i<32; i++)
    {
        tx_buf[i] = sendvalue[i];
    }
    nrf905.TX(tx_buf);
    
    delay(50);
}

</pre>
其中TX代码：
<pre>
void NRF905::TX(unsigned char *TxRxBuf)
{
    set_tx();
    delay(1);
    // Send data by nRF905
    TxPacket(config_info_buf+5, TxRxBuf);

}
</pre>
set_tx用于设置进入tx状态
TxPacket(config_info_buf+5, TxRxBuf);向write_config_buf结构的地址进行发送
<pre>
void NRF905::set_tx(void)
{
	digitalWrite(TRX_CE,LOW);
	digitalWrite(TXEN,HIGH);

	// delay for mode change(>=650us)
	delay(1);
}

void NRF905::TxPacket(unsigned char *TxAddress, unsigned char *TxRxBuf)
{
	int i;
	digitalWrite(CSN,LOW);
	// Write payload command
	SPI.transfer(WTP);
	for (i=0;i<32;i++){
	    // Write 32 bytes Tx data
		SPI.transfer(TxRxBuf[i]);
	}
	digitalWrite(CSN,HIGH);
	delay(1);

    // Spi enable for write a spi command
	digitalWrite(CSN,LOW);
	// Write address command
	SPI.transfer(WTA);
	// Write 4 bytes address
	for (i=0;i<4;i++){
		SPI.transfer(TxAddress[i]);
	}
	// Spi disable
	digitalWrite(CSN,HIGH);

	// Set TRX_CE high,start Tx data transmission, CE pulse
	digitalWrite(TRX_CE,HIGH);
	delay(1);
	digitalWrite(TRX_CE,LOW);
}
</pre>

向SPI中写数据时候要注意TRX_CE进行高低电平转换才会认为要进行数据发射




